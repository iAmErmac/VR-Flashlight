
Class S_FlashLightControl : EventHandler
{
	String set_playerstartweapon;
	String player_lastweapon;
	Int delay_autoactivate_flashlight;

	override void PlayerEntered(PlayerEvent e)
	{
		PlayerInfo plr = players[e.PlayerNumber];
		if(plr.ReadyWeapon) set_playerstartweapon = plr.ReadyWeapon.GetClassName();
		plr.mo.setinventory("S_FlashLight", 1, false);
	}
	
	override void NetworkProcess(ConsoleEvent e)
	{
		PlayerInfo plr = players[consoleplayer];
		if(!plr) return;
		
		if(e.Name == "fl_quick_toggle")
		{		
			if(!vr_flashlight_mount)
			{
				//Quickly switch to flashlight when key pressed
				
				if(plr.ReadyWeapon is "S_FlashLight" && player_lastweapon != "")
				{
					plr.mo.A_SelectWeapon(player_lastweapon);
				}
				if(!(plr.ReadyWeapon is "S_FlashLight"))
				{
					player_lastweapon = plr.ReadyWeapon.GetClassName();
					plr.mo.A_SelectWeapon("S_FlashLight");
					delay_autoactivate_flashlight = 25;
				}
			}
			else
			{
				//Or toggle flashlight on/off when weapon mounted
				
				let fl = TrackedSpotLight(S_FlashLight(plr.mo.findinventory("S_FlashLight")).f_light);
				
				if(fl.on)
				{
					S_StartSound("DDZ_Flashlight_Off", CHAN_WEAPON, CHAN_UI);
					fl.Deactivate(plr.mo);
				}
				else
				{
					S_StartSound("DDZ_Flashlight_On", CHAN_WEAPON, CHAN_UI);
					fl.Activate(plr.mo);
				}
			}
		}
	}
	
	override void WorldTick()
	{
		PlayerInfo plr = players[consoleplayer];
		if(!plr) return;
		
		if(delay_autoactivate_flashlight == 1)
		{
			S_StartSound("DDZ_Flashlight_On", CHAN_WEAPON, CHAN_UI);
			TrackedSpotLight(S_FlashLight(plr.mo.findinventory("S_FlashLight")).f_light).Activate(plr.mo);
		}
		if(delay_autoactivate_flashlight > 0)
		{
			delay_autoactivate_flashlight--;
		}
	
		if(!set_playerstartweapon) return;
		
		plr.mo.A_SelectWeapon(set_playerstartweapon);
		set_playerstartweapon = "";
	}
}

class TrackedSpotLight : Spotlight {

    enum ELocation
	{
		HELMET = 0,
		RIGHT_SHOULDER = 1,
		LEFT_SHOULDER = 2,
        CAMERA = 3
	};

    Actor toFollow;
	Double w_pitch;
    Vector3 offset;
    Vector3 finalOffset;
    bool ready;
    bool on;
    
    ELocation location;
    
    void updateFromCvars ()
	{
	    Color c = CVar.GetCVar("vr_flashlight_color").GetString();
    
        args[0] = c.r;
        args[1] = c.g;
        args[2] = c.b;    
        args[3] = CVar.GetCVar("vr_flashlight_intensity").GetInt();
        
        self.SpotInnerAngle = CVar.GetCVar("vr_flashlight_inner").GetFloat();        
        self.SpotOuterAngle = CVar.GetCVar("vr_flashlight_outer").GetFloat();
    }
    
    
    override void Activate(Actor activator)
	{
        updateFromCvars();
        on = true;
        super.Activate(activator);
    }
    
    
    override void DeActivate(Actor activator)
	{
        on = false;
        super.DeActivate(activator);
    }
        
    
    override void Tick()
	{
		if(!toFollow){
			Super.Tick();
			return;
		}
	
        if(ready && on)
		{
			offset = (0, 0, 0);	
			if(!w_pitch) w_pitch = 30;
			
            A_SetAngle(toFollow.angle, 0);
            Vector2 finalOffset2D = RotateVector ((offset.x, offset.y), toFollow.angle - 90.0);                         
            finalOffset = (finalOffset2D.x, finalOffset2D.y, offset.z);
            A_SetPitch(w_pitch, SPF_INTERPOLATE);
            SetOrigin(self.toFollow.pos + finalOffset, true);
        }
		else if(on && toFollow)
		{
            ready = true;
            //zBump = (toFollow.height - toFollow.viewheight) / 2;
        }
        Super.Tick();
    }
    
}

Class S_FlashLight : Fist
{
	Actor f_light;
	Double fl_pitch;
	Bool f_light_on;
	int fl_batt_counter;
	int fl_batt_capacity;
	int fl_batt_level;
	int fl_batt_low_blink;
	int fl_intensity;
	float fl_inner_angle;
	float fl_outer_angle;
	int fl_flicker;
	
	override void BeginPlay()
	{
		fl_batt_capacity = CVar.GetCVar("vr_flashlight_batt_cap").GetInt() * 60 * 35;
		fl_batt_counter = fl_batt_capacity;
		fl_batt_level = 10;
	}
	
	override void Tick()
	{
		Super.Tick();
		
		//If dropped somehow, stop the spotlight
		
		if(!owner)
		{
			if(f_light) f_light.destroy();
			return;
		}
		
		//On start of each level create a new spotlight
		
		if(!f_light)
		{
			f_light = Actor.Spawn("TrackedSpotLight");
			TrackedSpotLight(f_light).toFollow = self;
		}
		
		let fl = TrackedSpotLight(f_light);
		
		//Battery management routine
		
		fl_batt_capacity = CVar.GetCVar("vr_flashlight_batt_cap").GetInt() * 60 * 35;
		if(fl_batt_capacity && fl_batt_counter > fl_batt_capacity) fl_batt_counter = fl_batt_capacity;
		
		if(fl_batt_capacity > 0) //No battery management required if unlimited battery
		{
		
			if(fl_batt_counter > 0 && fl.on)
			{
				fl_batt_counter--;
			}
			else if(fl_batt_counter < fl_batt_capacity && !fl.on)
			{
				fl_batt_counter++;
			}
			else if(fl_batt_counter <= 0 && fl.on)
			{
				fl.Deactivate(owner);
				S_StartSound("DDZ_Flashlight_Off", CHAN_WEAPON, CHAN_UI);
			}
			
			if(vr_flashlight_batt_overlay)
			{
				if(fl_batt_counter > (fl_batt_capacity * 9/10))
				{
					fl_batt_level = 10;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 8/10))
				{
					fl_batt_level = 9;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 7/10))
				{
					fl_batt_level = 8;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 6/10))
				{
					fl_batt_level = 7;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 5/10))
				{
					fl_batt_level = 6;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 4/10))
				{
					fl_batt_level = 5;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 3/10))
				{
					fl_batt_level = 4;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 2/10))
				{
					fl_batt_level = 3;
				}
				else if(fl_batt_counter > (fl_batt_capacity * 1/10))
				{
					fl_batt_level = 2;
				}
				else
				{
					fl_batt_level = 1;
					
					//This is for the blinking power led when battery is almost empty
					
					if(fl_batt_low_blink <= 0)
						fl_batt_low_blink = 35;
					else
						fl_batt_low_blink--;					
				}
			}
			else
			{
				fl_batt_level = -1;
			}
		}
		else{
			fl_batt_level = -1;
		}
		
		//Dims flashlight a bit when low on battery
		
		if(vr_flashlight_dim)
		{
		
			if(fl_flicker <= 0)
				fl_flicker = 35;
			else
				fl_flicker--;
			
			if(fl_batt_level > 0 && fl_batt_level < 6 && !fl_flicker )
			{
				fl_intensity = CVar.GetCVar("vr_flashlight_intensity").GetInt();
				fl_inner_angle = CVar.GetCVar("vr_flashlight_inner").GetFloat();
				fl_outer_angle = CVar.GetCVar("vr_flashlight_outer").GetFloat();
				
				fl_intensity = ceil(fl_intensity * 0.9);
				
				if(fl_batt_level < 2)
				{
					fl_intensity = ceil(fl_intensity * frandom(0.4,0.6));
					fl_inner_angle *= frandom(0.2,0.4);
					fl_outer_angle *= frandom(0.8,0.9);
					fl_flicker = random(5,10);
				}
				else if(fl_batt_level < 3)
				{
					fl_intensity = ceil(fl_intensity * frandom(0.6,0.7));
					fl_inner_angle *= frandom(0.5,0.7);
					fl_outer_angle *= frandom(0.8,0.9);
					fl_flicker = random(10,17);
				}
				else if(fl_batt_level < 4)
				{
					fl_intensity = ceil(fl_intensity * frandom(0.6,0.7));
				}
				else if(fl_batt_level < 5)
				{
					fl_intensity = ceil(fl_intensity * frandom(0.7,0.8));
				}
			}
		}
		
		if(fl_batt_capacity == 0 || !vr_flashlight_dim)
		{
			fl_intensity = CVar.GetCVar("vr_flashlight_intensity").GetInt();
			fl_inner_angle = CVar.GetCVar("vr_flashlight_inner").GetFloat();
			fl_outer_angle = CVar.GetCVar("vr_flashlight_outer").GetFloat();
		}		
		if(fl_intensity) fl.args[3] = fl_intensity;
		if(fl_inner_angle) fl.SpotInnerAngle = fl_inner_angle;
		if(fl_outer_angle) fl.SpotOuterAngle = fl_outer_angle;
		
		//Turn off light when switched to a weapon
		
		if(!vr_flashlight_mount && owner.player.ReadyWeapon != self)
		{
			fl.DeActivate(owner);
			return;
		}
		
		//Flashlight beam tracer routine
		
		if(!fl.ready || !fl.on) return;
		
		Actor beam_tracer = owner.SpawnPlayerMissile("FlashLightBeamTracer");
		let bounce_light_tracer = owner.SpawnPlayerMissile("FlashLightBeamSpot");
		if(bounce_light_tracer) bounce_light_tracer.master = self;
		
		if(beam_tracer)
		{
			beam_tracer.master = self; //setting tracer beam as child so it can communicate back
			fl.toFollow = beam_tracer; //Spotlight will follow tracer beam
			if(fl_pitch) fl.w_pitch = fl_pitch;
		}
	}
	
	override void DetachFromOwner()
	{
		if(f_light) f_light.destroy();
		Super.DetachFromOwner();
	}
	
	action void A_Flashlight()
	{
		let fl = TrackedSpotLight(invoker.f_light);
		
		if(!fl.on)
		{
			fl.Activate(invoker.owner);
			S_StartSound("DDZ_Flashlight_On", CHAN_WEAPON, CHAN_UI);
		}
		else
		{
			fl.DeActivate(invoker.owner);
			S_StartSound("DDZ_Flashlight_Off", CHAN_WEAPON, CHAN_UI);
		}		
	}
	
	action void A_CheckModel()
	{
		Int model_type = CVar.GetCVar("vr_flashlight_model").GetInt();
		let fl = TrackedSpotLight(invoker.f_light);
		
		String w_sprite;
		if(model_type == 1)
		{
			if(fl && fl.on)
				w_sprite = "FLTE";
			else
				w_sprite = "FLTN";
		}
		else
		{
			if(fl && fl.on)
				w_sprite = "FLTV";
			else
				w_sprite = "FLVN";
		}
			
		if (player) Player.GetPSprite(PSP_WEAPON).sprite = GetSpriteIndex(w_sprite);
	}
	
	action void A_CheckPickupModel()
	{
		Int model_type = CVar.GetCVar("vr_flashlight_pickup").GetInt();
		
		String p_sprite;
		if(model_type == 0)
			p_sprite = "FLSH";
		else if(model_type == 1)
			p_sprite = "FLTV";
		else
			p_sprite = "FLTE";

		invoker.sprite = GetSpriteIndex(p_sprite);
		invoker.A_SetScale(0.7);
	}
	
	Default
	{
	+WEAPONSPAWN
	+WEAPON.NOALERT;
	+INVENTORY.UNTOSSABLE;
	+INVENTORY.UNDROPPABLE;
	+THRUACTORS;
	//+NOCLIP;
	weapon.selectionorder 4141;
	inventory.maxamount 1;
	}

	States
	{
	Spawn:
		FLSH P 1 A_CheckPickupModel();
		Loop;
	Ready:
		FLTE A 0 A_CheckModel();
		"####" A 0 A_Overlay(1001, "BatteryOverlay");
		"####" A 1 A_WeaponReady;
		Loop;
	Deselect:
		FLTE A 0 A_CheckModel();
		"####" A 1 A_Lower(60);
		Loop;
	Select:
		FLTE A 0 A_CheckModel();
		"####" A 1 A_Raise(60);
		Loop;
	Fire:
		"####" A 0 A_FlashLight();
		"####" AAAAAAAAAAAAAAA 1 A_Overlay(1001, "BatteryOverlay");
		Goto Ready;
	BatteryOverlay:
		FLBT A 0 A_JumpIf(vr_flashlight_model == 0, "VBatteryOverlay");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 0, "BatteryLevel0");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 1, "BatteryLevel1");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 2, "BatteryLevel2");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 3, "BatteryLevel3");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 4, "BatteryLevel4");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 5, "BatteryLevel5");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 6, "BatteryLevel6");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 7, "BatteryLevel7");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 8, "BatteryLevel8");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 9, "BatteryLevel9");
		FLBT A 0 A_JumpIf(invoker.fl_batt_level == 10, "BatteryLevel10");
		Stop;
	BatteryLevel0:
		FLBT A 1;
		Stop;
	BatteryLevel1:
		FLBT A 0 A_JumpIf(invoker.fl_batt_low_blink < 18, "BatteryLevel0");
		FLBT B 1;
		Stop;
	BatteryLevel2:
		FLBT C 1;
		Stop;
	BatteryLevel3:
		FLBT D 1;
		Stop;
	BatteryLevel4:
		FLBT E 1;
		Stop;
	BatteryLevel5:
		FLBT F 1;
		Stop;
	BatteryLevel6:
		FLBT G 1;
		Stop;
	BatteryLevel7:
		FLBT H 1;
		Stop;
	BatteryLevel8:
		FLBT I 1;
		Stop;
	BatteryLevel9:
		FLBT J 1;
		Stop;
	BatteryLevel10:
		FLBT K 1;
		Stop;
	VBatteryOverlay:
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 0, "VBatteryLevel0");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 1, "VBatteryLevel1");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 2, "VBatteryLevel2");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 3, "VBatteryLevel3");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 4, "VBatteryLevel4");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 5, "VBatteryLevel5");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 6, "VBatteryLevel6");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 7, "VBatteryLevel7");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 8, "VBatteryLevel8");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 9, "VBatteryLevel9");
		FLBV A 0 A_JumpIf(invoker.fl_batt_level == 10, "VBatteryLevel10");
		Stop;
	VBatteryLevel0:
		FLBV A 1;
		Stop;
	VBatteryLevel1:
		FLBV A 0 A_JumpIf(invoker.fl_batt_low_blink < 18, "VBatteryLevel0");
		FLBV B 1;
		Stop;
	VBatteryLevel2:
		FLBV C 1;
		Stop;
	VBatteryLevel3:
		FLBV D 1;
		Stop;
	VBatteryLevel4:
		FLBV E 1;
		Stop;
	VBatteryLevel5:
		FLBV F 1;
		Stop;
	VBatteryLevel6:
		FLBV G 1;
		Stop;
	VBatteryLevel7:
		FLBV H 1;
		Stop;
	VBatteryLevel8:
		FLBV I 1;
		Stop;
	VBatteryLevel9:
		FLBV J 1;
		Stop;
	VBatteryLevel10:
		FLBV K 1;
		Stop;
	Dummy:
		FLTN A 0;
		FLTV A 0;
		FLVN A 0;
		Stop;
	}
	
	static const statelabel bl_labels[] =
	{
	"BatteryLevel1", "BatteryLevel2", "BatteryLevel3", "BatteryLevel4", "BatteryLevel5", "BatteryLevel6", "BatteryLevel7", "BatteryLevel8", "BatteryLevel9", "BatteryLevel10"
	};
}

Class FlashLightBeamTracer : Actor
{
	Vector3 t_pos;
	double t_pitch;
	
	override void BeginPlay()
	{
		t_pos = self.pos;
	}

	override void Tick()
	{
		Super.Tick();
		
		let dX = t_pos.x - self.pos.x;
		let dY = t_pos.y - self.pos.y;
		let dZ = t_pos.z - self.pos.z;
		
		//let t_pitch = atan2(sqrt(dZ * dZ + dX * dX), dY); //Didn't work for some reason
		t_pitch = (atan2(sqrt(dX * dX + dY * dY), dZ) * -1) + 90; //idk how the fuck I made this work, but it works!
		
		S_FlashLight(master).fl_pitch = t_pitch; //update tracer pitch info in parent actor
	}

	Default
	{
	Projectile;
	+MISSILE;
	+NOGRAVITY;
	+NOBLOCKMAP;
	+DONTSPLASH;
	+THRUACTORS;
	//+NOCLIP;
	Radius 1;
	Height 1;
	Damage 0;
	Speed 65;
	RenderStyle "None";
	}
	
	States
	{
	Spawn:
		TNT1 A 1; //don't need more than a single tick
		Stop;
	}
}

//Dynamic light actors to use when flashlight hits the floor

Class FlashLightBeamSpot : Actor
{
	Default
	{
	Projectile;
	+MISSILE;
	+NOGRAVITY;
	+NOBLOCKMAP;
	+DONTSPLASH;
	+THRUACTORS;
	Renderstyle "None";
	Radius 10;
	Height 10;
	Damage 0;
	Speed 65;
	}
	
	States
	{
	Spawn:
		TNT1 A 1;
		Stop;
	Death:
	XDeath:
	Crash:
		//TNT1 A 0 A_CheckFloor("NoLightSpot");
		//TNT1 A 0 A_CheckCeiling("NoLightSpot");
		TNT1 A 0 A_JumpIf(invoker.master && S_FlashLight(invoker.master).fl_batt_level == 2, "SmallLightSpot");
		TNT1 A 0 A_JumpIf(invoker.master && S_FlashLight(invoker.master).fl_batt_level == 1, "SmallerLightSpot");
		TNT1 A 1 A_SpawnItem("FlashLightSpot");
		Stop;
	SmallLightSpot:
		TNT1 A 1 A_SpawnItem("FlashLightSpotSmall");
		Stop;
	SmallerLightSpot:
		TNT1 A 1 A_SpawnItem("FlashLightSpotSmaller");
		Stop;
	NoLightSpot:
		TNT1 A 0;
		Stop;
	}
}

Class FlashLightSpot : Actor
{
	Default
	{
	+NOGRAVITY;
	//+NOCLIP;
	+NOBLOCKMAP;
	+DONTSPLASH;
	+THRUACTORS;
	Renderstyle "None";
	Radius 10;
	Height 10;
	Damage 0;
	}
	
	States
	{
	Spawn:
		TNT1 A 3; //Why do you need 3 ticks to spawn dynamic light?
		Stop;
	}
}

Class FlashLightSpotSmall : FlashLightSpot {}
Class FlashLightSpotSmaller : FlashLightSpot {}